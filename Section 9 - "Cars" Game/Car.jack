/** Handles a game car */
class Car {
    
    field int xInitialPos, xCurrentPos, yInitialPos, speed, direction, radius;

    /** Constructs a new car given its initial position, speed, direction and radius, and draws it on the screen */
    constructor Car new(int Ix, int Iy, int s, int d, int r) {

        let xInitialPos = Ix;
        let xCurrentPos = xInitialPos;
        let yInitialPos = Iy;
        let speed = s;
        let direction = d;
        let radius = r;

        /* draw the car */
        do Screen.setColor(true);
        do Screen.drawCircle(xInitialPos, yInitialPos, radius);

        return this;
    }

    /** Disposes this car */
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    /** Moves this car up by one pixel in its direction */
    method void moveCar() {

        /* remove the car in its current position from the screen */
        do Screen.setColor(false);
        do Screen.drawCircle(xCurrentPos, yInitialPos, radius);

        if(direction = 0) {     // if direction is left

            if(xCurrentPos > (30 + radius)) {
                let xCurrentPos = xCurrentPos - speed;
            }

            else {      // reached end of screen - change direction
                let direction = 1;
                let xCurrentPos = xCurrentPos + speed;
            }
        }

        else {

            if(xCurrentPos < (478 - radius)) {
                let xCurrentPos = xCurrentPos + speed;
            }

            else {      // reached end of screen - change direction
                let direction = 0;
                let xCurrentPos = xCurrentPos - speed;
            }
        }

        /* draw the car in its new position */
        do Screen.setColor(true);
        do Screen.drawCircle(xCurrentPos, yInitialPos, radius);

        return;
    }

    /** Gets the current x-axis position of this car */
    method int getXCurrentPos() {
        return xCurrentPos;
    }

    /** Gets the current y-axis position of this car */
    method int getYCurrentPos() {
        return yInitialPos;
    }

    /** Gets the radius of this car */
    method int getRadius() {
        return radius;
    }
}